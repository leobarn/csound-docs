<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">

<html>

<head>
<meta http-equiv="Content-Type"
content="text/html; charset=iso-8859-1">
<meta name="GENERATOR" content="Microsoft FrontPage 2.0">
<title>70_01_1.orc.html</title>
<meta name="FORMATTER" content="Microsoft FrontPage 2.0">
</head>

<body stylesrc="../admin/adporc.html"
background="../admin/AccciDefaultBackground.gif"
bgcolor="#FFFFFF">

<pre>; *********************************************************************
; ACCCI:     70_01_1.ORC
; synthesis: Granular Synthesis(70)
;            Basic Granular Synthesis(01)
;            Sinus as source for individual grains(1)
; coded:     Ron K. Parks 6/96

sr=44100
kr=441
ksmps=100
nchnls=1


instr 1; **************************************************************

idur    = p3     ; duration for one stream of grains

iampfn  = p4     ; function number for overall envelope (per stream)
iboctfn = p5     ; grain center fq function number in oct notation

ibwfn   = p6     ; bandwidth of random frequency variation of grains,
                 ; 1 = one semitone variation above and below base fq
imiigtfn= p7     ; minimum inter-grain time function number
imxigtfn= p8     ; maximum inter-grain time function number
imiglfn = p9     ; minimum grain length function number
imxglfn = p10    ; maximum grain length function number
iseed   = p11    ; seed value for randh
igefn   = p12    ; grain envelope function number


; **************************************** grain pitch control signal
; baseoct (center fq) in oct notation
kboct   oscil1   0, 1, idur, iboctfn

; bandwidth of random variation around baseoct
; 1 = one semitone variation above and below the base fq
kbw     oscil1   0, 1, idur, ibwfn

; random variation of grain pitch
; mean = kboct, range = +-kbw/12
kvar    randh    kbw/12, kr, iseed+.03

; **************************************** duration of grain + silence
; inter-grain time                         ; output = inter-grain time
kmiigt  oscil1   0, 1, idur, imiigtfn      ; randomly selected between
kmxigt  oscil1   0, 1, idur, imxigtfn      ; minimum and maximum
kigtr    =       (kmxigt-kmiigt)/2         ; inter-grain time
kr1     randh    kigtr, kr, iseed+.01            
kigt    =        kmiigt+(kr1+kigtr)

; grain length                             ; output = grain length
kmigl   oscil1   0, 1, idur, imiglfn       ; randomly selected between
kmxgl   oscil1   0, 1, idur, imxglfn       ; minimum and maximum
kglr    =        (kmxgl-kmigl)/2           ; grain length
kr2     randh    kglr, kr, iseed+.01            
kgl     =        kmigl+(kr2+kglr)

; get table length of grain envelope function 
itablen =        ftlen(igefn)  

; ***** The next for statements are computed at init time
; ***** and also at p-time after being called by 'reinit' 
; ***** 'reinit' allows multiple inits during one note event

reset:	
        iigt     =        i(kigt)       ; inter-grain time
        igl      =        i(kgl)        ; grain length
        itotdur  =        igl+iigt      ; total duration of pass
						
        icps     =        cpsoct(i(kboct)+i(kvar)) ; get grain pitch

; ***** The 'timout' statement branches off to perform 'grain' and
; ***** all statements until endin.  

                 timout   0, itotdur , grain       ; go to grain
                                                   
; ***** 'Reinit' begins a special init pass during p-time.
; ***** It continues through rireturn or endin, whichever first
; ***** It behaves like any init pass: updating i-variables and
; ***** initializing certain modules, here: 'timout' and 'line'
; ***** Note: In oscillators reinit causes a phase reinit, which in
; *****       this case does not matter because there is an envelope
; *****       controlling the oscillator - otherwise you can omit
; *****       rireturn and let the i-pass continue though 'endin'

                 reinit   reset                    ; go to reset
		 
grain:  andx     line     0, igl, itablen    ; index for table read  
        agenv    tablei   andx, igefn        ; grain envelope
        a1       oscili   agenv, icps, 1     ; grain signal
        rireturn                             ; end of reinit pass

        kenv     oscil1   0, 1, idur, iampfn ; envelope of grain stream

                 out      a1*kenv            ; end of grain pass here
endin                                        ; continue with reinit</pre>
</body>
</html>
