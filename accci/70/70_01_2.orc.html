<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">

<html>

<head>
<meta http-equiv="Content-Type"
content="text/html; charset=iso-8859-1">
<meta name="GENERATOR" content="Microsoft FrontPage 2.0">
<title>70_01_2.orc.html</title>
<meta name="FORMATTER" content="Microsoft FrontPage 2.0">
</head>

<body stylesrc="../admin/adporc.html"
background="../admin/AccciDefaultBackground.gif"
bgcolor="#FFFFFF">

<pre>; *********************************************************************
; ACCCI:     70_01_2.ORC
; synthesis: Granular Synthesis(70)
;            Basic Granular Synthesis(01)
;            Using soundfile as grain source, uniform pseudo random
;            selection of grain start point, inter-grain time, and
;            grain length (2)
; coded:     Ron K. Parks 6/96

sr=44100
kr=441
ksmps=100
nchnls=1


instr 1; **************************************************************

idur    = p3     ; duration for one stream of grains
itrnsfn = p4     ; function number for grain transposition (semitones)
                 ; 12 = one octave higher, -12 = one octave lower
iampfn  = p5     ; function number for overall envelope (per stream)
ifns    = p6     ; grain soundfile source table number
imiigtfn= p7     ; minimum inter-grain time function number
imxigtfn= p8     ; maximum inter-grain time function number
imiglfn = p9     ; minimum grain length function number
imxglfn = p10    ; maximum grain length function number
iseed   = p11    ; seed value for randh
igefn   = p12    ; grain envelope function number
irvarfn = p13    ; function for amount of random variation of
                 ; grain start point around read pointer (in sec)
ifnrp   = p14    ; line function for read pointer


; ****************************************** duration grain machine
; inter-grain time                         
kmiigt  oscil1   0, 1, idur, imiigtfn      ; randomly selected between
kmxigt  oscil1   0, 1, idur, imxigtfn      ; minimum and maximum
kigtr    =       (kmxigt-kmiigt)/2         ; inter-grain time
kr1     randh    kigtr, kr, iseed+.01            
kigt    =        kmiigt+(kr1+kigtr)
; grain length                             
kmigl   oscil1   0, 1, idur, imiglfn       ; randomly selected between
kmxgl   oscil1   0, 1, idur, imxglfn       ; minimum and maximum
kglr    =        (kmxgl-kmigl)/2           ; grain length
kr2     randh    kglr, kr, iseed+.02            
kgl     =        kmigl+(kr2+kglr)

; get table length of grain envelope function (only once)
itablen =        ftlen(igefn)  

; ************** compute beginning and end of read pointer

krpt    oscil1   0, 1, idur, ifnrp
krvar   oscil1   0, 1, idur, irvarfn
kr3     randh    krvar, kr, iseed+ .03
kbegg   =        (krpt+kr3)*1000*44.1
ktrns   oscil1   0, 1, idur, itrnsfn
kgtrns  =        (exp((ktrns*100)/1731.2345))*1000
kendg   =        kbegg+(kgl*kgtrns*44.1)

  

; ***** The next five statements are computed at init time
; ***** and also at p-time after being called by 'reinit' 
; ***** 'reinit' allows multiple inits during one note event

reset:  ibegg    =        i(kbegg)      ; begin grain read
        iendg    =        i(kendg)      ; end grain read			

        iigt     =        i(kigt)       ; inter-grain time
        igl      =        i(kgl)        ; grain length
        itotdur  =        igl+iigt      ; duration of timout

; ***** The 'timout' statement branches off to perform 'grain' and
; ***** all statements until endin.  

                 timout   0, itotdur , grain       ; go to grain
                                                   
; ***** 'Reinit' begins a special init pass during p-time.
; ***** It continues through rireturn or endin, whichever first.
; ***** It behaves like any init pass: updating i-variables and
; ***** initializes certain modules, here: timout, line and the 
; ***** phase of the grain oscillator
; ***** Then normal performance resumes.

                 reinit   reset                    ; go to reset
		 
grain:  andxge   line     0, igl, itablen    ; index grain envelope 
        agenv    tablei   andxge, igefn      ; read grain envelope
        andxst   line     ibegg, igl, iendg  ; index soundfile table
        a1       tablei   andxst,ifns,0,0,0  ; read from soundfile

                 rireturn                    ; end reset pass
	   
        kenv     oscil1   0, 1, idur, iampfn ; grain stream envelope
                 out      a1*agenv*kenv      ; end grain pass
endin                                       </pre>
</body>
</html>
